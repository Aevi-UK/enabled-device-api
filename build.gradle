buildscript {
    apply from: 'dependencies.gradle'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {

    ext.applyScript = { String script ->
        try {
            apply from: new File(rootDir, "gradle-scripts/${script}.gradle").path
        } catch (Exception e) {
            ext."${script}" = null
            project.logger.log(LogLevel.WARN, e.message)
        }
    }

    ext.publicationGroup = "com.aevi.dms"
    ext.versionName = "1.1.1"
    ext.versionCode = 1005

    project.ext {
        branch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
        sha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

        getVersionName = { suffix = '' ->
            if (!branch || branch.equals('master')) {
                return "${project.versionName}${suffix}"
            } else if (branch.startsWith('release/')) {
                "${project.versionName}${suffix}-RC1"
            } else {
                "${project.versionName}${suffix}-SNAPSHOT-${sha}"
            }
        }
    }
    
    repositories {
        google()
        jcenter()
        maven {
            url "http://sdk-public-artifacts.s3-website-eu-west-1.amazonaws.com/maven2"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
